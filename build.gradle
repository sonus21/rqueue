plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'nebula.optional-base' version '5.0.2'
}
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.optional-base'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
    }
}
ext {
    springBootVersion = System.getenv("SPRING_BOOT_VERSION")
    springVersion = System.getenv("SPRING_VERSION")
    springDataVersion = System.getenv("SPRING_DATA_VERSION")
    microMeterVersion = System.getenv("MICROMETER_VERSION")

    // logging dependencies
    lombokVersion = '1.18.10'
    logbackVersion = '1.2.3'
    sl4jVersion = '1.7.28'

    // testing
    junitVersion = '4.12'
    mockitoVersion = '3.1.0'
    hamcrestVersion = '2.2'
    powerMockVersion = '2.0.5'
    jacocoVersion = '0.8.4'
    embeddedRedisVersion = '0.7.2'
    h2Version = '1.4.194'
    tomcatVersion = '8.0.47'

    // utility
    lang3Version = '3.9'
    jacksonVersion = '2.10.0'

    // server
    javaxServletVersion = '4.0.1'


    // database
    lettuceVersion = '5.2.0.RELEASE'
    javaxAnnotationVersion = '1.3.2'
    jpaVersion = '2.2'
    hibernateVersion = '5.4.9.Final'
}


subprojects {
    group = 'com.github.sonus21'
    version = '1.4.0-RELEASE'

    dependencies {
        // https://mvnrepository.com/artifact/org.springframework/spring-messaging
        compile group: 'org.springframework', name: 'spring-messaging', version: "${springVersion}"
        // https://mvnrepository.com/artifact/org.springframework.data/spring-data-redis
        compile group: 'org.springframework.data', name: 'spring-data-redis', version: "${springDataVersion}"

        testCompile group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
        testCompile group: 'ch.qos.logback', name: 'logback-core', version: "${logbackVersion}"
        // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"
        testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
        // https://mvnrepository.com/artifact/org.mockito/mockito-core
        testCompile group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
        // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-library
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: "${hamcrestVersion}"
        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        testCompile group: 'org.apache.commons', name: 'commons-lang3', version: "${lang3Version}"
        // https://mvnrepository.com/artifact/org.springframework/spring-test
        testCompile group: 'org.springframework', name: 'spring-test', version: "${springVersion}"
        testCompile("org.springframework.boot:spring-boot-test:${springBootVersion}")
        // https://mvnrepository.com/artifact/org.powermock/powermock-module-junit4
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: "${powerMockVersion}"

        // https://mvnrepository.com/artifact/org.powermock/powermock-api-mockito2
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: "${powerMockVersion}"

        configurations {
            all*.exclude module: 'spring-boot-starter-logging'
        }
    }
}

def publishedProjects = subprojects.findAll({ subproject ->
    subproject.pluginManager.hasPlugin('java') && !subproject.name.endsWith("example") && !subproject.name.contains("test")
})

task jacocoMerge(type: JacocoMerge) {
    publishedProjects.each { subproject ->
        executionData subproject.tasks.withType(Test)
    }
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}


task codeCoverageReport(type: JacocoReport, group: 'verification', description: 'Generate code coverage report') {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    publishedProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        csv.enabled false
        html.enabled true
    }
    test.useTestNG()
}


jacoco {
    toolVersion = "${jacocoVersion}"
}

clean.doLast {
    subprojects.findAll({ subproject ->
        def _files = file(subproject.name + '/log').listFiles()
        if (_files != null) {
            delete _files
        }
    })
};

codeCoverageReport.dependsOn {
    subprojects*.test
}

coveralls {
    description = 'Uploads the aggregated coverage report to Coveralls'
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

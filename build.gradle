plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'nebula.optional-base' version '5.0.2'
    id "com.adarshr.test-logger" version "2.1.1"
    id 'org.gradle.test-retry' version '1.0.0'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'org.gradle.test-retry'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }
}

ext {
//    springBootVersion = System.getenv("SPRING_BOOT_VERSION")
//    springVersion = System.getenv("SPRING_VERSION")
//    springDataVersion = System.getenv("SPRING_DATA_VERSION")
//    microMeterVersion = System.getenv("MICROMETER_VERSION")

    springBootVersion = '3.0.0'
    springVersion = '6.0.3'
    springDataVersion = '3.0.0'
    microMeterVersion = '1.10.2'

    // logging dependencies
    lombokVersion = '1.18.24'
    logbackVersion = '1.4.5'
    sl4jVersion = '2.0.6'

    // testing
    jupiterVersion = '5.5.0'
    mockitoVersion = '3.5.0'
    hamcrestVersion = '2.2'
    jacocoVersion = '0.8.4'
    embeddedRedisVersion = '0.7.2'
    h2Version = '1.4.194'
    tomcatVersion = '10.1.4'

    // utility
    lang3Version = '3.9'
    jacksonVersion = '2.14.1'

    // server
    jakartaServletVersion = '6.0.0'
    pebbleVersion = '3.1.6'

    // database
    lettuceVersion = '6.2.2.RELEASE'
    jakartaAnnotationVersion = '2.1.0'
    jakartaPersistenceVersion = '3.1.0'
    hibernateVersion = '6.1.6.Final'

    // other dependencies
    jakartaValidationApiVersion = '3.0.2'

    guavaVersion = '30.1-jre'
}

subprojects {
    group = 'com.github.sonus21'
    version = '3.0.0-RELEASE'

    dependencies {
        // https://mvnrepository.com/artifact/org.springframework/spring-messaging
        api group: 'org.springframework', name: 'spring-messaging', version: "${springVersion}"
        // https://mvnrepository.com/artifact/org.springframework.data/spring-data-redis
        api group: 'org.springframework.data', name: 'spring-data-redis', version: "${springDataVersion}"

        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: "${lang3Version}"

        // https://mvnrepository.com/artifact/com.google.guava/guava
        implementation group: 'com.google.guava', name: 'guava', version: "${guavaVersion}"

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
        testImplementation group: 'ch.qos.logback', name: 'logback-core', version: "${logbackVersion}"
        // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
        testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${jupiterVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${jupiterVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${jupiterVersion}"
        // https://mvnrepository.com/artifact/org.mockito/mockito-inline
        testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoVersion}"
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"
        // https://mvnrepository.com/artifact/org.hamcrest/hamcrest
        testImplementation group: 'org.hamcrest', name: 'hamcrest', version: "${hamcrestVersion}"
        testImplementation("org.springframework.boot:spring-boot-test:${springBootVersion}")
        testImplementation group: 'org.junit-pioneer', name: 'junit-pioneer', version: '0.9.0'

        configurations {
            all*.exclude module: 'spring-boot-starter-logging'
            all*.exclude module: 'junit'
        }
    }
}

def publishedProjects = subprojects.findAll({ subproject ->
    subproject.pluginManager.hasPlugin('java') && !subproject.name.endsWith("example") && !subproject.name.contains("test")
})

task codeCoverageReport(type: JacocoReport, group: 'verification', description: 'Generate code coverage report') {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/reports/jacoco/*.exec")

    publishedProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        csv.enabled = false
        html.enabled = System.getenv("CIRCLECI") != "true"
        xml.enabled = System.getenv("CIRCLECI") == "true"
        xml.destination = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            'com/github/sonus21/rqueue/exception',
                            'com/github/sonus21/rqueue/models/response',
                            'com/github/sonus21/rqueue/core/RqueueMessageSender*',
                            'com/github/sonus21/rqueue/utils/PrefixLogger*',
                            'com/github/sonus21/rqueue/utils/StackTraceUtil*',
                            'com/github/sonus21/rqueue/core/ScheduledTaskDetail*',
                    ]
            )
        }))
    }
    dependsOn(subprojects*.test)
    doLast {}
    test.useTestNG()
}


jacoco {
    toolVersion = "${jacocoVersion}"
}

clean.doLast {
    subprojects.findAll({ subproject ->
        def _files = file(subproject.name + '/log').listFiles()
        if (_files != null) {
            delete _files
        }
    })
}

coveralls {
    description = 'Uploads the aggregated coverage report to Coveralls'
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}
